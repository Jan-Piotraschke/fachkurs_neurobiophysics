load_file("Axon_Neuron.hoc")

maxsyn = 2

// create a post synaptic neuron
create target_cell
target_cell {
     L  = 30
     diam = 30
     insert pas
}

// make the target_cell visible
target_cell {
     define_shape()
     for i=0, n3d() - 1 {
          pt3dchange(i, x3d(i) + 100, y3d(i), z3d(i), diam3d(i))
     }
}

objref electrode
soma { 
       electrode = new IClamp(0.5) 
       electrode.del = 10 
       electrode.dur = 10
       electrode.amp = 4.5
}  

objref con_list
con_list = new List() 

objref syn[maxsyn]
target_cell syn[0] = new MySynapse(0.5) 
apic[11] syn[1] = new MySynapse(0.5) 


// Terminal is considered here as the name of the part of the axon which is the furthest from the soma of L5PC the axon terminal.
axon con_list.append(new NetCon(&v(1), syn[0], 3, 2, 0.01)) 
axon con_list.append(new NetCon(&v(1), syn[1], 3, 2, 0.01)) 

for i = 0, con_list.count()-1 { con_list.object(i).weight = 0.006 }
forall nseg = int(L + 1) 


objref sp
sp = new PlotShape()

tstop = 50

objref data_v_soma, data_v_terminal, data_v_target, data_v_apic11, data_time
data_v_soma = new Vector()
data_v_terminal = new Vector()
data_v_target = new Vector()
data_v_apic11 = new Vector()

data_time = new Vector()

data_time.record(&t)

data_v_soma.record(&soma.v(0.5))
data_v_terminal.record(&axon.v(0.5))
data_v_target.record(&target_cell.v(0.5))
data_v_apic11.record(&apic[11].v(0.5))

run()

// define the plot
objref voltage_plot
voltage_plot = new Graph()

voltage_plot.size(0, data_time.max(), data_v_soma.min(), data_v_soma.max())

// plot the data
data_v_soma.line(voltage_plot, data_time, 1, 2)
data_v_terminal.line(voltage_plot, data_time, 2, 2)
data_v_target.line(voltage_plot, data_time, 3, 2)
data_v_apic11.line(voltage_plot, data_time, 4, 2)

voltage_plot.label("Soama (black)")
voltage_plot.label("Terminal (red)")
voltage_plot.label("Target (blue)")
voltage_plot.label("Apic11 (green)")


voltage_plot.exec_menu("View = plot") // to autoscale the graph


objref file
file = new File()
file.wopen("data.csv")

objref data_matrix
data_matrix = new Matrix()
data_matrix.resize(data_v_soma.size(),5)
data_matrix.setcol(0, data_time)
data_matrix.setcol(1, data_v_soma)
data_matrix.setcol(2, data_v_terminal)
data_matrix.setcol(3, data_v_target)
data_matrix.setcol(4, data_v_apic11)

data_matrix.fprint(file, " %g")
file.close()